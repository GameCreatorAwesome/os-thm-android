apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "3.0.2"
}

def getArtifactId = { ->
    return "osthm"
}

def getProjectDirectory = { ->
    return "lib";
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'tw'
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getProjectDirectory()}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ThatCakeID/os-thm-android")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

bintray {
    user = githubProperties['bintray.usr'] ?: System.getenv('BINTRAY_USER')
    key = githubProperties['bintray.key'] ?: System.getenv('BINTRAY_API_KEY')
    publications = ['bar']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'os-thm-android'
        name = 'os-thm'
        description = "Open Source Theme for Android"
        publish = true
        publicDownloadNumbers = true
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/ThatCakeID/os-thm-android.git'
        dryRun = true
        version {
            name = getVersionName()
            desc = "os-thm ${getVersionName()}"
            released = new Date()
            vcsTag = getVersionName()
        }
    }
}